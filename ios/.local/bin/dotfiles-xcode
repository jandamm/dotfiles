#!/usr/bin/env zsh

set -e

selection=$1

function set_xcode_info() {
	xcode_path=$(xcode-select -p)
	xcode_version=${${xcode_path#*/Xcode-}%\.app*}
}

function is_installed() {
	[[ -e /Applications/Xcode-$1.app ]]
}

# ensure full semver: 1.2.3
if [[ -n $selection ]]; then
	selection=( ${(ps:.:)selection} )
	selection="$selection[1].${selection[2]:-0}.${selection[3]:-0}"
fi

set_xcode_info

# selected current or none and current is not patched
if [[ $selection = $xcode_version ]]; then
	echo "Using current Xcode version: \"$xcode_version\""
else

	# selected none and current is patched
	if [[ -z $selection ]]; then
		xcodes select

	# selected version is installed
	elif is_installed $selection; then
		xcodes select $selection

	# selected version is not installed
	else
		xcodes install $selection
		xcodes select $selection
		xcode-select --install
	fi

	set_xcode_info
	echo "Using \"Xcode $xcode_version\""
fi
