#!/usr/bin/env zsh

zparseopts -D -E f=force -force=force || exit

dir=${XDG_DATA_HOME:-$HOME/.local/share}/nvim/sumneko/repo
name=lua-language-server
lsp=${dir:h}/$name
bin=/usr/local/bin/$name

# https://github.com/sumneko/lua-language-server/wiki/Build-and-Run-(Standalone)

function current_head() {
	git log --max-count=1 --format=%h
}

# clone project
if [[ ! -d $dir ]]; then
	git clone https://github.com/sumneko/lua-language-server $dir
	initial=true
fi
cd $dir
current=$(current_head)
git pull

if [[ -z $force && -z $initial && $current = $(current_head) ]]; then
	exit 0
fi

git submodule update --init --recursive
echo

# compile
cd 3rd/luamake
compile/install.sh
cd ../..
./3rd/luamake/luamake rebuild

# Create and link executable
echo "$dir/bin/lua-language-server $dir/main.lua" > $lsp
chmod +x $lsp

unlink $bin &>/dev/null
ln -s $lsp $bin
